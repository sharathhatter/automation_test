buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath "com.newrelic.agent.android:agent-gradle-plugin:5.+"
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

repositories {
    mavenCentral()
    maven {
        url 'https://maven.fabric.io/public'
    }
}

repositories {
    mavenCentral()
    maven {
        url "http://dl.bintray.com/journeyapps/maven"
    }
}

repositories {
    jcenter()
    maven { url 'http://maven.localytics.com/public' }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'newrelic'
import com.android.build.OutputFile

android {
    compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION)
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION

    signingConfigs {
        bigbasket {
            storeFile project.KEY_STORE_FILE != "NONE" ? file(project.KEY_STORE_FILE) : null
            storePassword project.STORE_PASSWD
            keyAlias project.KEY_ALIAS
            keyPassword project.KEY_PASSWD
        }
    }

    defaultConfig {
        applicationId "com.bigbasket.mobileapp"
        minSdkVersion Integer.parseInt(project.ANDROID_BUILD_MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)
        versionCode Integer.parseInt(project.VERSION_CODE)
        versionName project.VERSION_NAME
        manifestPlaceholders = [crashlytics_id: "d2ddc47cb01fb54a209f0ff02a2aecfec7b4d827"]
        //Does not work on gradle build tool version 1.3.0 https://code.google.com/p/android/issues/detail?id=182016
        setProperty("archivesBaseName", "Bigbasket-" + versionName);
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [crashlytics_id: "5c333c62dd3801a51b2d6c14ef6e11a868ba5277"]
            signingConfig signingConfigs.bigbasket.storeFile != null ? signingConfigs.bigbasket : null
        }
        debug {
            multiDexEnabled true
            signingConfig signingConfigs.bigbasket
        }
    }
    productFlavors {
        // For faster startup when building MultiDex app on a lollypop device
        lollypopDev {
            minSdkVersion 21
        }
        // For other devices
        regularDev {
            minSdkVersion Integer.parseInt(project.ANDROID_BUILD_MIN_SDK_VERSION)
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
    dexOptions {
        jumboMode true
        javaMaxHeapSize "2g"
    }

    lintOptions {
        abortOnError false
    }

    splits {
        density {
            enable project.ENABLE_SPLITS != null ? project.ENABLE_SPLITS.toBoolean() : false;
            exclude "ldpi", "tvdpi"
            compatibleScreens 'small', 'normal', 'large', 'xlarge'
        }
    }


}

ext.versionCodes = [mdpi   : calculateVersionCodeFromDpi(160, 0),
                    hdpi   : calculateVersionCodeFromDpi(240, 0),
                    xhdpi  : calculateVersionCodeFromDpi(320, 0),
                    xxhdpi : calculateVersionCodeFromDpi(480, 0),
                    xxxhdpi: calculateVersionCodeFromDpi(640, 0),
                    all    : calculateVersionCodeFromDpi(160, -1)]

android.applicationVariants.all { variant ->
    // assign different version code for each output
    variant.outputs.each { output ->
        def key = output.getFilter(OutputFile.DENSITY) == null ? "all" : output.getFilter(OutputFile.DENSITY)
        output.versionCodeOverride = project.ext.versionCodes.get(key)
    }
}

int calculateVersionCodeFromDpi(int dpi, int delta) {
    int minSdk = Integer.parseInt(project.ANDROID_BUILD_MIN_SDK_VERSION)
    int versionCode = Integer.parseInt(project.VERSION_CODE) + delta
    return ("" + minSdk + (dpi / 10) + versionCode).toInteger()
}


dependencies {
    compile files('libs/localytics.jar')
    compile('com.crashlytics.sdk.android:crashlytics:2.5.3@aar') {
        transitive = true;
    }
    compile project(':wibmoSDK')
    compile files('libs/AF-Android-SDK-v2.3.1.17.jar')
    compile project(':moe-addon-ubox')
    compile project(':mobikwik-sdk')
    compile files('libs/pgsdk.jar')
    compile project(':payuSdkUi')
    compile files('libs/GoogleConversionTrackingSdk-2.2.4.jar')
    compile "com.squareup.picasso:picasso:${picassoLibVersion}"
    compile "com.android.support:design:${supportLibVersion}"
    compile "com.android.support:recyclerview-v7:${supportLibVersion}"
    compile 'com.squareup.retrofit:retrofit:2.0.0-beta2'
    compile 'com.squareup.retrofit:converter-gson:2.0.0-beta2'
    provided 'com.squareup.okhttp:okhttp:2.6.0'
    compile "com.google.android.gms:play-services-ads:${playServicesLibVersion}"
    compile "com.google.android.gms:play-services-plus:${playServicesLibVersion}"
    compile "com.google.android.gms:play-services-gcm:${playServicesLibVersion}"
    compile "com.google.android.gms:play-services-location:${playServicesLibVersion}"
    compile 'com.newrelic.agent.android:android-agent:5.+'
    compile 'com.facebook.android:facebook-android-sdk:4.4.0'
    compile 'com.journeyapps:zxing-android-embedded:3.0.1@aar'
    compile 'com.google.zxing:core:3.2.0'
    compile 'com.moengage:moe-android-sdk:5.3.14'
    compile 'com.facebook.fresco:fresco:0.8.1+'
    compile 'com.facebook.fresco:imagepipeline-okhttp:0.8.1+'
    compile "com.android.support:support-v4:${supportLibVersion}"
    debugCompile "com.squareup.leakcanary:leakcanary-android:${leakCanaryLibVersion}"
    debugCompile 'com.android.support:multidex:1.0.1'
    debugCompile 'com.squareup.okhttp:logging-interceptor:2.6.0'
}